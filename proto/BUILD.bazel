load("@io_bazel_rules_go//go:def.bzl", "go_library")
load("@io_bazel_rules_go//proto:def.bzl", "go_proto_library")
load("@io_bazel_rules_dotnet//dotnet:defs.bzl", "core_library", "DOTNET_CORE_FRAMEWORKS")

## Used for documentation
genrule(
    name = "docs",
    srcs = [
        "solarium.proto",
        "//pkg/gamemodes/desert-planet/proto:desert.proto",
        "//pkg/gamemodes/thewolfgame/proto:wolf.proto",
    ],
    outs = ["docs.md"],
    cmd = """
        $(location @com_google_protobuf//:protoc) \
            --plugin=protoc-gen-doc=$(location @com_github_pseudomuto_protoc_gen_doc//cmd/protoc-gen-doc:protoc-gen-doc) \
            --doc_out=$(GENDIR)/proto \
            --doc_opt=markdown,docs.md \
            $(SRCS)
    """,
    tools = [
        "@com_google_protobuf//:protoc",
        "@com_github_pseudomuto_protoc_gen_doc//cmd/protoc-gen-doc:protoc-gen-doc",
    ],
)

## End Docs
proto_library(
    name = "proto_proto",
    srcs = ["solarium.proto"],
    visibility = ["//visibility:public"],
    deps = [
        "//pkg/gamemodes/desert-planet/proto:proto_proto",
        "//pkg/gamemodes/thewolfgame/proto:proto_proto",
    ],
)

go_proto_library(
    name = "proto_go_proto",
    compilers = ["@io_bazel_rules_go//proto:go_grpc"],
    importpath = "github.com/chronojam/solarium/proto",
    proto = ":proto_proto",
    visibility = ["//visibility:public"],
    deps = [
        "//pkg/gamemodes/desert-planet/proto:go_default_library",
        "//pkg/gamemodes/thewolfgame/proto:go_default_library",
    ],
)

go_library(
    name = "go_default_library",
    embed = [":proto_go_proto"],
    importpath = "github.com/chronojam/solarium/proto",
    visibility = ["//visibility:public"],
)

# C#
load("@build_stack_rules_proto//csharp:csharp_proto_compile.bzl", "csharp_proto_compile")
load("@build_stack_rules_proto//csharp:csharp_grpc_library.bzl", "csharp_grpc_library")
load("@build_stack_rules_proto//csharp:csharp_grpc_compile.bzl", "csharp_grpc_compile")

csharp_grpc_compile(
    name = "csharp_grpc",
    deps = [
        ":proto_proto",
    ],
)

[core_library(
    name = "solarium_csharp_coreapp_{}".format(framework),
    srcs = [
        ":csharp_grpc",
        "//pkg/gamemodes/desert-planet/proto:csharp_grpc",
    ],
    dotnet_context_data = "@io_bazel_rules_dotnet//:core_context_data_{}".format(framework),
    deps = [
        "@io_bazel_rules_dotnet//dotnet/stdlib.core/{}:system.io.dll".format(framework),
        "@io_bazel_rules_dotnet//dotnet/stdlib.core/{}:netstandard.library.dll".format(framework),
        "@google.protobuf//:netcoreapp2.1_core",
        "@grpc.core//:netcoreapp2.1_core",
        "@grpc.core.api//:netcoreapp2.1_core",
        "@system.interactive.async//:netcoreapp2.1_core",
    ],
    visibility = ["//visibility:public"],
) for framework in DOTNET_CORE_FRAMEWORKS]

[core_library(
    name = "solarium_csharp_netstandard_{}".format(framework),
    srcs = [
        ":csharp_grpc",
        "//pkg/gamemodes/desert-planet/proto:csharp_grpc",
    ],
    dotnet_context_data = "@io_bazel_rules_dotnet//:core_context_data_{}".format(framework),
    deps = [
        "@io_bazel_rules_dotnet//dotnet/stdlib.core/{}:system.io.dll".format(framework),
        "@io_bazel_rules_dotnet//dotnet/stdlib.core/{}:netstandard.library.dll".format(framework),
        "@google.protobuf//:netstandard2.0_net",
        "@grpc.core//:netstandard2.0_net",
        "@grpc.core.api//:netstandard2.0_net",
        "@system.interactive.async//:netstandard2.0_net",
    ],
    visibility = ["//visibility:public"],
) for framework in DOTNET_CORE_FRAMEWORKS]
